import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("com.bmuschko:gradle-docker-plugin:3.0.5")
        classpath("io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.springframework.build.gradle:propdeps-plugin:0.0.7')
    }
}

// fixes spring boot 1.5 auto configuration from thymeleaf 2 to 3
ext['thymeleaf.version'] = '3.0.6.RELEASE'
ext['thymeleaf-layout-dialect.version'] = '2.0.0'


apply plugin: 'io.spring.dependency-management'

allprojects {
    group = 'org.opentestsystem.rdw.reporting'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'

    if (project.hasProperty("buildNumber")) {
        project.setVersion(project.version.split("-")[0] + "." + buildNumber)
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {

        dependencies {
            dependency 'org.opentestsystem.rdw.common:rdw-common-messaging:0.0.1-SNAPSHOT'
            dependency 'org.opentestsystem.rdw.common:rdw-common-model:0.0.1-SNAPSHOT'
            dependency 'org.opentestsystem.rdw.common:rdw-common-utils:0.0.1-SNAPSHOT'
            dependency 'org.opentestsystem.rdw.common:rdw-common-security:0.0.1-SNAPSHOT'
            dependency 'org.opentestsystem.rdw.common:rdw-common-status:0.0.1-SNAPSHOT'
            dependency 'org.opentestsystem.rdw.common:rdw-common-archive:0.0.1-SNAPSHOT'

            dependency 'com.google.guava:guava:21.0'
            dependency 'org.apache.commons:commons-io:1.3.2'

            dependency 'com.fasterxml.jackson.core:jackson-annotations:2.8.0'
            dependency 'com.fasterxml.jackson.core:jackson-databind:2.8.7'
            dependency 'org.springframework:spring-context:4.3.7.RELEASE'
            dependency 'org.thymeleaf:thymeleaf:3.0.6.RELEASE'

            dependency 'org.springframework.security.extensions:spring-security-saml2-core:1.0.2.RELEASE'
        }
        imports {
            // spring cloud Camden.SR5 includes spring cloud stream Brooklyn.SR2
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR5'
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:1.7+"

        testCompile 'junit:junit:4+'
        testCompile 'org.assertj:assertj-core:3.6+'
        testCompile 'org.mockito:mockito-all:1.10.19'
    }

    // generate coverage report automatically (./build/reports/jacoco/test/html/index.html)
    test.finalizedBy(jacocoTestReport)

    // task to generate build-info.properties with build.version, build.timestamp
    // using this approach instead of resource filtering to avoid modifying source
    task createBuildInfoFile(dependsOn: processResources) {
        doLast {
            def buildInfoFile = new File("${buildDir}/resources/main/build-info.properties")
            buildInfoFile.parentFile.mkdirs()
            Properties props = new Properties()
            props.setProperty('build.version', project.version.toString())
            props.store(buildInfoFile.newWriter(), null)
        }
    }

    // task to create Dockerfile
    // (http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/Dockerfile.html)
    task createDockerfile(type: Dockerfile) {
        destFile = project.file('build/docker/Dockerfile')
        from('java:8-jre-alpine')
        volume('/tmp')
        label([maintainer: 'Fairway Technologies'])
        copyFile("${jar.archivePath.name}", "${jar.archivePath.name}")
        exposePort(8008, 8080)
        entryPoint("java", "-jar", "${jar.archivePath.name}")
    }

    test.exclude '**/*IT.*'

    //This task for for manually running the integration tests against schemas being developed simultaneously with this
    // project, versus the schemas that are "fixed" under the RDW_Schema submodule in this project
    task manualIT(type: Test) {
        outputs.upToDateWhen { false }
        doFirst {
            println "Running Manual Integration Tests..."
        }
    }

    //The omission of the ':' in manualIT is on purpose - we need the relative path to manualIT
    task IT(type: Test, dependsOn:[":cleanReportingTest", ":migrateReportingTest"]) {
        outputs.upToDateWhen { false }
        doLast {
            println "Running Integration Tests..."
        }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }
}

task migrateReportingTest(type: GradleBuild) {
    group = "Schema"
    description = "Cleans the schemas that integration tests depend on"
    dir = "RDW_Schema"
    tasks = ["migrateReporting_test"]
}

task cleanReportingTest(type: GradleBuild) {
    group = "Schema"
    description = "Migrates the schemas that integration tests depend on"
    dir = "RDW_Schema"
    tasks = ["cleanReporting_test"]
}
